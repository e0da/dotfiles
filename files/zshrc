if [ -f /etc/zprofile ]; then
  source /etc/zprofile
fi

# add local bin path to PATH
#
export PATH=${HOME}/bin:${HOME}/opt/bin:${HOME}/Dropbox/bin:${PATH}

export DARWIN=`uname | grep Darwin`

# intial auto config
#
zstyle ':completion:*' auto-description '%d'
zstyle ':completion:*' completer _expand _complete _ignored _correct _approximate
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' format '%d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent pwd .. directory
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list '' 'l:|=* r:|=*' 'r:|[._-]=** r:|=**' 'm:{[:lower:]}={[:upper:]} m:{[:lower:][:upper:]}={[:upper:][:lower:]}'
zstyle ':completion:*' menu select=0
zstyle ':completion:*' original true
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' verbose true
zstyle :compinstall filename '~/.zshrc'

autoload -Uz compinit
compinit

# disable zsh which. I HATE IT.
disable which

# editor
#
export EDITOR=vi
export GIT_EDITOR=vi

# C-x C-e command editing
#
autoload -U edit-command-line
zle -N edit-command-line
bindkey -e
bindkey '^X^E' edit-command-line
bindkey '^R' history-incremental-pattern-search-backward

# history
#
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory autocd beep extendedglob nomatch notify autopushd

# Fix C-[left] C-[right] behavior
#
bindkey ';5C' forward-word
bindkey ';5D' backward-word

# aliases
#
if [ ${DARWIN} ]; then
  alias ls='ls -G'
  alias go=open
else
  alias ls='ls --color=auto'
  alias go=gnome-open
fi

alias grep='grep --color=auto'
alias g='git'
alias h='heroku'
alias gg='gitg&!'

## ric
RIC=~/code/work/ric
alias pview="python ${RIC}/remote_capture/pickle_to_cap.py"
export PYTHONPATH=${RIC}/src:${PYTHONPATH}
export PATH=${RIC}/src:${RIC}/tools:${PATH}

function workon_ric() {
  echo ${PS1} | grep ric2.5 > /dev/null
  if [ $? -eq 1 ]; then
    workon ric2.5
  fi
}

function ric() {
  cd ${RIC}
  workon_ric
}

function pickles() {
  ric
  cd src/nutricate/tests/rparse/pickles
}

# enable pasting of command lines beginning with $. When do you ever mean to
# start a command with '$'?
#
# source: http://stackoverflow.com/questions/68372/what-is-your-single-most-favorite-command-line-trick-using-bash
function $ {
  $@
}

# Dirty hack for gvim not integrating with unity when launched from command line
#   https://bugs.launchpad.net/ubuntu/+source/vim/+bug/776499
function gvim() {
  /usr/bin/gvim -f $* &!
}

# show all git aliases
#
function git_aliases() {
  git config --get-regexp alias | sed 's/^alias\.//g' | awk '{printf "%7s %s %s %s %s %s %s %s %s %s\n", $1, $2, $3, $4, $5, $6, $7, $8, $9, $10}'
}

# kill specified line from .ssh/known_hosts
#
function khk() {
  sed -i ${1}d ~/.ssh/known_hosts
}

# wrap knife ssh with my constant arguments since their config file doesn't
# support it FOR SOME RAISIN
#
function kssh() {
  knife ssh -x justinforce -a name $*
}

##
# Creates a .rvmrc.
#
# Arguments:
#   $1 ruby version
#     - Defaults to 1.9.3.
#     - You may specify "default" to explicitly use the default.
#   $2 gemset name
#     - Defaults to $(basename `pwd`)
#     - You must also specify a ruby version to specify a gemset. You can
#       specify "default" for your ruby version to use the default.
#
function rvmrc() {
  default_ruby='1.9.3'
  default_gemset=$(basename `pwd`)
  : ${1:=$default_ruby}
  : ${2:=$default_gemset}
  [[ "$1" == 'default' ]] && 1=$default_ruby
  rvm --install --create --rvmrc ${argv[1]}@${argv[2]}
  cd .
  rvm current
}

##
# Opens an ssh tunnel to repo01 and opens a browser to RabbitMQ management
# interface.
#
# Arguments:
#   $1: local port to listen on (defaults to 55672)
#
function rmq() {
  : ${1=55672}
  local_port=$1
  ssh repo01 -fNL${local_port}:flint-mq01:55672
  url=http://localhost:$local_port
  open $url || gnome-open $url
}

# command not found
#
if [ -f /etc/zsh_command_not_found ]; then
  source /etc/zsh_command_not_found
fi

# prompts
#
export PROMPT='%B%F{cyan}%m%F{white}:%F{blue}%~%F{white}%#%b%f '

## zsh-git-prompt
#
source ~/.zsh/git-prompt/zshrc.sh
export ZSH_THEME_GIT_PROMPT_NOCACHE=1
export RPROMPT='$(git_super_status)'

# The render width of these special characters gets doinked in Gnome Terminal,
# so append a space.
#
export ZSH_THEME_GIT_PROMPT_STAGED="$ZSH_THEME_GIT_PROMPT_STAGED "
export ZSH_THEME_GIT_PROMPT_CONFLICTS="$ZSH_THEME_GIT_PROMPT_CONFLICTS "
export ZSH_THEME_GIT_PROMPT_CHANGED="$ZSH_THEME_GIT_PROMPT_CHANGED "
export ZSH_PROMPT_CHARS_ALREADY_PATCHED=1

# automatically configure make -j option to -j{number of CPUs +1}
#
if [ ! ${DARWIN} ]; then
  let MAKEJOBS=$(grep processor /proc/cpuinfo|wc -l)+1
  export MAKEOPTS=-j${MAKEJOBS}
fi

# rvm
#
[[ -s "${HOME}/.rvm/scripts/rvm" ]] && . "${HOME}/.rvm/scripts/rvm"  # This loads RVM into a shell session.
export PATH=${PATH}:${HOME}/.rvm/bin # Add RVM to PATH for scripting


# alias hub as git
#
# This MUST come after adding ~/bin to PATH AND after loading rvm.
#
eval "$(hub alias -s)"


if [ -x ~/.maven_env ]; then
  source ~/.maven_env
fi

# wtf? Why does rxvt-unicode set TERM=rxvt-unicode-256color? It breaks all
# kinds of shit, and it works fine with TERM=xterm-color. Dumb.
#
export TERM=xterm-color
